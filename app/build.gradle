apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.gitlab.arturbosch.detekt'

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.javernaut.whatthecodec"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 3500
        versionName "3.5.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    lintOptions{
        disable 'ExtraTranslation'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerVersion rootProject.kotlinVersion
        kotlinCompilerExtensionVersion rootProject.composeVersion
    }
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true
    }
    signingConfigs {
        google {
            if (!rootProject.ext["signing.google.alias"].isEmpty()) {
                keyAlias rootProject.ext["signing.google.alias"]
                keyPassword rootProject.ext["signing.google.passowrd"]
                storeFile file(rootProject.ext["signing.google.storeFile"])
                storePassword rootProject.ext["signing.google.storePassword"]
            }
        }
        amazon {
            if (!rootProject.ext["signing.amazon.alias"].isEmpty()) {
                keyAlias rootProject.ext["signing.amazon.alias"]
                keyPassword rootProject.ext["signing.amazon.passowrd"]
                storeFile file(rootProject.ext["signing.amazon.storeFile"])
                storePassword rootProject.ext["signing.amazon.storePassword"]
            }
        }
        huawei {
            if (!rootProject.ext["signing.huawei.alias"].isEmpty()) {
                keyAlias rootProject.ext["signing.huawei.alias"]
                keyPassword rootProject.ext["signing.huawei.passowrd"]
                storeFile file(rootProject.ext["signing.huawei.storeFile"])
                storePassword rootProject.ext["signing.huawei.storePassword"]
            }
        }
    }
    flavorDimensions "market"
    productFlavors {
        google {
            dimension "market"
            ndk {
                // Since the App Bundle is used, there is no problem in packaging all these ABIs
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
            signingConfig signingConfigs.google
        }
        amazon {
            dimension "market"
            applicationIdSuffix ".amzn"
            ndk {
                // Amazon Appstore doesn't support multiple APKs for non-Amazon devices.
                // There is no point in x86 support here, as the majority of devices with the
                // Amazon Appstore are ARM-based. And it seems to be a common practice for other
                // apps in this market.
                abiFilters 'armeabi-v7a'
            }
            signingConfig signingConfigs.amazon
        }
        huawei {
            dimension "market"
            applicationIdSuffix ".huawei"
            ndk {
                // Huawei App Gallery supports App Bundle format
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
            signingConfig signingConfigs.huawei
        }
        ci {
            dimension "market"
            ndk {
                abiFilters 'x86'
            }
        }
    }
}

dependencies {
    implementation "io.github.javernaut:mediafile:1.0.3"

    def coroutines_version = "1.5.2"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'com.google.android.material:material:1.5.0'

    def lifecycle_version = "2.4.1"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.savedstate:savedstate:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.browser:browser:1.4.0'

    // TODO replace with own implementation
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    // Compose
    implementation "androidx.compose.runtime:runtime:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui:$rootProject.composeVersion"
    implementation "androidx.compose.foundation:foundation:$rootProject.composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$rootProject.composeVersion"
    implementation "androidx.compose.material:material:$rootProject.composeVersion"
    implementation "androidx.compose.material:material-icons-extended:$rootProject.composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$rootProject.composeVersion"
    implementation "androidx.activity:activity-compose:1.4.0"
    implementation "com.google.accompanist:accompanist-pager:$rootProject.accompanistVersion"
    implementation "com.google.accompanist:accompanist-pager-indicators:$rootProject.accompanistVersion"
}

detekt {
    config = files("../config/detekt/detekt.yml")
    buildUponDefaultConfig = true
}