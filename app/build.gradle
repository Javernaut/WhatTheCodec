apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.gitlab.arturbosch.detekt'

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.javernaut.whatthecodec"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 3421
        versionName "3.4.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerVersion rootProject.kotlinVersion
        kotlinCompilerExtensionVersion rootProject.composeVersion
    }
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true
    }
    flavorDimensions "market"
    productFlavors {
        google {
            dimension "market"
            ndk {
                // Since the App Bundle is used, there is no problem in packaging all these ABIs
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
        }
        amazon {
            dimension "market"
            applicationIdSuffix ".amzn"
            ndk {
                // Amazon Appstore doesn't support multiple APKs for non-Amazon devices.
                // There is no point in x86 support here, as the majority of devices with the
                // Amazon Appstore are ARM-based. And it seems to be a common practice for other
                // apps in this market.
                abiFilters 'armeabi-v7a'
            }
        }
        huawei {
            dimension "market"
            applicationIdSuffix ".huawei"
            ndk {
                // Huawei App Gallery supports App Bundle format
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            }
        }
        ci {
            dimension "market"
            ndk {
                abiFilters 'x86'
            }
        }
    }
}

dependencies {
    implementation project(path: ':media-file')

    def coroutines_version = "1.5.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'com.google.android.material:material:1.4.0'

    def lifecycle_version = "2.3.1"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"

    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.savedstate:savedstate:1.1.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.browser:browser:1.3.0'

    // TODO replace with own implementation
    implementation "com.github.hadilq.liveevent:liveevent:1.2.0"

    // Compose
    implementation "androidx.compose.runtime:runtime:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui:$rootProject.composeVersion"
    implementation "androidx.compose.foundation:foundation:$rootProject.composeVersion"
    implementation "androidx.compose.foundation:foundation-layout:$rootProject.composeVersion"
    implementation "androidx.compose.material:material:$rootProject.composeVersion"
    implementation "androidx.compose.material:material-icons-extended:$rootProject.composeVersion"
    implementation "androidx.compose.runtime:runtime-livedata:$rootProject.composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$rootProject.composeVersion"
    implementation "androidx.activity:activity-compose:1.3.0"
    implementation "com.google.accompanist:accompanist-pager:$rootProject.accompanistVersion"
    implementation "com.google.accompanist:accompanist-pager-indicators:$rootProject.accompanistVersion"
}

repositories {
    mavenCentral()
}

detekt {
    config = files("../config/detekt/detekt.yml")
    buildUponDefaultConfig = true
}